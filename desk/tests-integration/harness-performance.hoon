::  Pure production hot-path timings. No prompts, network or agent evaluation.
::  -test /=harness=/tests-integration/harness-performance
/-  h=harness, hh=harness-hand, c=harness-corpus, *harness-store
/+  *test, hl=harness, hs=harness-session, hd=harness-hand, ci=harness-corpus, si=harness-session-index, defaults=harness-defaults
|%
++  history
  |=  turns=@ud
  ^-  (list event:h)
  =/  log=(list event:h)  ~[[%config-replaced builtin-config:defaults]]
  |-  ^-  (list event:h)
  ?:  =(0 turns)  log
  =/  id=@uv  `@uv`turns
  $(turns (dec turns), log [[%command-completed id 'memory' 'No pinned notes.'] [%input-received [id [%acp 'benchmark'] `~zod ~ ~2026.9.10 [%user '/memory']]] log])
++  fleet
  |=  count=@ud
  ^-  (map session-id:h session:h)
  =/  log  (history 256)
  =|  sessions=(map session-id:h session:h)
  |-  ^-  (map session-id:h session:h)
  ?:  =(0 count)  sessions
  =/  sid  (cat 3 'benchmark-' (scot %ud count))
  $(count (dec count), sessions (~(put by sessions) sid [log 1]))
++  test-hot-path-costs
  =/  saved=state-21  *state-21
  =.  sessions.saved  (fleet 128)
  =/  packed=vase  !>(saved)
  =/  validated
    ~>  %bout.[1 'perf-validate-full-store-128x513']
    !<(state-21 packed)
  =/  log  (history 4.096)
  =/  view
    ~>  %bout.[1 'perf-replay-8193-events']
    (play:hl log)
  =/  snapshot
    ~>  %bout.[1 'perf-unchanged-snapshot-8193-events']
    (snapshot:hs [log 1] `8.193)
  =/  full
    ~>  %bout.[1 'perf-first-history-page-8193-events']
    (history:hs [log 1] ~)
  =/  corpus
    ~>  %bout.[1 'perf-initial-corpus-capture-128']
    (sync:ci *state:c sessions.saved)
  =/  next  (~(put by sessions.saved) 'benchmark-1' [[[%memory-set 'note' `'updated'] (history 256)] 1])
  =/  modified
    ~>  %bout.[1 'perf-one-session-modified-index-128']
    (update:si sessions.saved next (seed:si sessions.saved) ~2026.9.10)
  =/  synced
    ~>  %bout.[1 'perf-one-session-corpus-sync-128']
    (sync:ci corpus next)
  ;:  weld
    (expect-eq !>(sessions.saved) !>(sessions.validated))
    (expect-eq !>(8.193) !>(revision.view))
    (expect !>(?=(%o -.snapshot)))
    (expect !>(?=(^ before.full)))
    (expect-eq !>(128) !>(~(wyt by modified)))
    (expect-eq !>(128) !>(~(wyt by names.synced)))
  ==
++  ledger
  |=  count=@ud
  ^-  state:hh
  =|  db=state:hh
  |-  ^-  state:hh
  ?:  =(0 count)  db
  =/  id=@uv  `@uv`count
  =.  observations.db
    (~(put by observations.db) id ['binding' 'event' 'actor' 'body' (add ~2026.9.10 (mul count ~s1)) %completed])
  =.  outbox.db
    (~(put by outbox.db) id [id 'binding' ?:(=(0 (mod count 2)) 'other-hand' 'one-hand') 'room' 'session' %reply 'retained output' ?:(=(count 1) %pending %delivered) '' '' ~])
  $(count (dec count))
++  test-pending-frontier-with-retained-evidence
  =/  db  (ledger 2.048)
  =/  before
    ~>  %bout.[1 'perf-outbox-sort-all-2048']
    =/  sorted
      %+  sort  ~(tap by outbox.db)
      |=  [a=[@uv publication:hh] b=[@uv publication:hh]]
      (lth at:(~(got by observations.db) -.a) at:(~(got by observations.db) -.b))
    (skim sorted |=([id=@uv pub=publication:hh] &(=('one-hand' hand.pub) =(%pending status.pub))))
  =/  after
    ~>  %bout.[1 'perf-outbox-pending-frontier-2048']
    (pending-publications:hd db 'one-hand')
  (expect-eq !>(before) !>(after))
--
